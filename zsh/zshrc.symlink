[[ -f "$HOME/Projects/drowsier/zshrc.zsh" ]] && . "$HOME/Projects/drowsier/zshrc.zsh"

log() {
  # echo -e $1
}

log '~/.zshrc'

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Projects

# initialize autocomplete here, otherwise functions won't be loaded
# also load compdef so git-completion doesn't complain about no compdef
autoload -U compinit compdef
compinit

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)
# exclude oh my zsh
config_files=(${config_files:#$ZSH/oh-my-zsh/*})

log "\nLoad the path files"
for file in ${(M)config_files:#*/path.zsh}; do
  log $file
  source $file
done

log "\nLoad everything but the path and completion files"
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  log $file
  source $file
done

log "\nLoad every completion after autocomplete loads"
for file in ${(M)config_files:#*/completion.zsh}; do
  log $file
  source $file
done

unset config_files

export PATH="$HOME/.yarn/bin:$HOME/.config/yarn/global/node_modules/.bin:$PATH"
